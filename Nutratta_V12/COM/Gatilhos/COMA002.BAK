#include "rwmake.ch"  
#include "topconn.ch"  
 
/*
========================================================================================================================
Rotina----: COMA002
Autor-----: Davidson Carvalho
Data------: 24/01/2018
========================================================================================================================
Descrição-:  Gera cod. sequencial automaticamente para o produto
Uso-------:  Chamado no gatilho do campo B1_C_CDFAM SX7
========================================================================================================================
*/
User Function COMA002()

Local _cCodPro 	:= "00000000"
Local _cArea 	:= GetArea()
Local _cQuery 	:= ""

_cQuery := " SELECT MAX(B1_COD) AS CODIGO "
_cQuery += " FROM " + RetSqlName("SB1")
_cQuery += " WHERE LEFT(B1_COD,2) = '" + SX5->X5_CHAVE + "'" 
_cQuery += " AND  D_E_L_E_T_ = ' '"

TcQuery _cQuery New Alias "TRB"
dbSelectArea("TRB")
dbGoTop()

_cCodPro := Soma1(Alltrim(TRB->CODIGO))

dbSelectArea("TRB")
 DBCloseArea("TRB")

While !MayIUseCode("B1_COD"+xFilial("SB1")+_cCodPro)
	_cCodPro := Soma1(_cCodPro)
EndDo             

RestArea(_cArea)

Return(_cCodPro)


/*
========================================================================================================================
Rotina----: NUT_VALDPRD
Autor-----: Validação do codigo do produto.
Data------: 24/01/2018
========================================================================================================================
Descrição-: Validação do codigo do produto.
Uso-------: 
========================================================================================================================
*/                     

User Function NUT_VALDPRD()   
      
Local cGrupo		:=	M->B1_GRUPO
Local cFamilia	:=	M->B1_C_CDFAM
Local lRet			:=	.T.
            
//Aviso("Nutratta","Chave virada para o Banco: "+cCodBco+Chr(13)+Chr(10)+" Operação realizada com sucesso!!",{"OK"},2)

/*
Grupo 2000 esolher familia Bovinos,Ovinos..Equinos
outros grupos levar o grupo 00-Não PA*/
                                               
//--Validação do conteudo do campo B1_C_FAMIL        
dbSelectArea("SX5")
dbSetOrder(1)
If dbSeek(xFilial("SX5")+'1X'+cFamilia)

	//Não permitir prosseguir se o grupo for 2000 e familia 00-NAO PA - OUTRO.
	If cGrupo $ '2000' .And. '00' $ Alltrim(cFamilia)
		Aviso("Nutratta","Para produto acabado escolher a familia correta:",{"Voltar"},2)
		lRet:=.F.
	ElseIf cGrupo # '2000' .And. '00' # Alltrim(cFamilia)
		Aviso("Nutratta","Favor selecionar a familia 'NAO PA - OUTRO' para este grupo de produto.",{"Voltar"},2)
		lRet:=.F.
	EndIf
Else
	Aviso("Nutratta","Favor informar uma familia de produtos valida:",{"Voltar"},2)
	lRet:=.F.
EndIf

Return(lRet)
